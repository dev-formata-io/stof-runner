//
// Copyright 2024 Formata, Inc. All rights reserved.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//    http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
//

// make sure the Users root exists
root Users: {}

type User {
    username: str;

    #[private]
    password: str;

    // 0b(exec, delete, write, read)
    perms: int = 0b0000;

    // modify scope
    // if set, this user can only modify this registry within this scope
    scope: str = '';

    fn authenticated(password: str): bool {
        return self.password == password;
    }
    fn can_read(): bool {
        return self.perms & 0b0001 > 0;
    }
    fn can_write(): bool {
        return self.perms & 0b0010 > 0;
    }
    fn can_delete(): bool {
        return self.perms & 0b0100 > 0;
    }
    fn can_exec(): bool {
        return self.perms & 0b1000 > 0;
    }
    fn can_modify_scope(path: str): bool {
        let user_scope = self.scope;
        if (user_scope.len() < 1) return true;

        let scope = path.split('/').first();
        if (scope.startsWith('@')) scope = scope.substring(1);
        return scope == user_scope;
    }
}

obj Admin: {
    // default export JSON path
    export_json_path: 'registry/__users__.json'

    // set a user
    fn set_user(username: str, password: str, perms: int = 0b1111, scope: str = ''): bool {
        Users.removeField(username, true);
        return Users.set(username, new User {
            username: username,
            password: password,
            perms: perms,
            scope: scope,
        });
    }

    // delete a user by username
    fn delete_user(username: str): bool {
        return Users.removeField(username, true);
    }

    // export users to a json file
    fn export_json_users(path: str = root.Admin.export_json_path) {
        let json = stringify(Users, 'json');
        fs.write(path, json);
    }
}

// authenticate a user by username, returning the user if present
fn authenticate(username: str, password: str): User {
    let user: User = Users.at(username);
    if (user && user.authenticated(password)) {
        return user;
    }
    return null;
}

// can this user read?
fn can_read(username: str, password: str): bool {
    let user = self.authenticate(username, password);
    return user && user.can_read();
}

// can this user write?
fn can_write(username: str, password: str, path: str = ''): bool {
    let user = self.authenticate(username, password);
    return user && user.can_write() && (path.len() < 1 || user.can_modify_scope(path));
}

// can this user delete?
fn can_delete(username: str, password: str, path: str = ''): bool {
    let user = self.authenticate(username, password);
    return user && user.can_delete() && (path.len() < 1 || user.can_modify_scope(path));
}

// can this user exec?
fn can_exec(username: str, password: str): bool {
    let user = self.authenticate(username, password);
    return user && user.can_exec();
}
